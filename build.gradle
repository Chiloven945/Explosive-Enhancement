plugins {
	id 'fabric-loom' version '1.7-SNAPSHOT'
	id 'maven-publish'
	id "dev.kikugie.j52j" version "1.0.2"
	id "dev.kikugie.stonecutter"
}

//sourceCompatibility = JavaVersion.VERSION_21
//targetCompatibility = JavaVersion.VERSION_21
//archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

def mcVersion = stonecutter.current.version
//def mcDep = property("mod.supported_mc_versions").toString()
def mcDep = project.supported_mc_versions.toString()

version = "${property('mod_version')}-${property('jarname_target')}"

base {
	archivesName = project.archives_base_name
}

loom {
	runConfigs.all {
		ideConfigGenerated true // Run configurations are not created for subprojects by default
		runDir "../../run" // Use a shared run folder and create separate worlds
	}
	configurations.all {
		resolutionStrategy {
			force("net.fabricmc:fabric-loader:$loader_version")
		}
	}
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.terraformersmc.com/releases/" }
	maven { url = "https://api.modrinth.com/maven"}
	maven {
		name 'Xander Maven'
		url 'https://maven.isxander.dev/releases'
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	//Mod Menu
	modApi "com.terraformersmc:modmenu:${modmenu_version}"

	//YACL
	if(stonecutter.eval(mcVersion, "=1.19.4")) {
		modImplementation "dev.isxander.yacl:yet-another-config-lib-fabric:${yacl_version}"
	} else {
		modImplementation "dev.isxander:yet-another-config-lib:${yacl_version}"
	}


	// Uncomment the following line to enable the deprecated Fabric API modules.
	// These are included in the Fabric API production distribution and allow you to update your mod to the latest modules at a later more convenient time.

	// modImplementation "net.fabricmc.fabric-api:fabric-api-deprecated:${project.fabric_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version, 'mcdep': project.property('supported_mc_versions'), 'javadep': stonecutter.eval(mcVersion, ">=1.20.5") ? '>=21' : '>=17'
	}

//	val refmap = "refmap" to "${mod.name}-$mcVersion-$loader-refmap.json"
//	inputs.properties(refmap)

	filesMatching("explosiveenhancement.mixins.json5") {
//		expand "refmap": "explosive-enhancement-$project.version-$project.jarname_target-refmap.json"
		expand "refmap": "explosive-enhancement-refmap.json"
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = stonecutter.eval(mcVersion, ">=1.20.5")
			? 21 : 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	def java = stonecutter.eval(mcVersion, ">=1.20.5")
			? JavaVersion.VERSION_21 : JavaVersion.VERSION_17
	sourceCompatibility = java
	targetCompatibility = java
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
